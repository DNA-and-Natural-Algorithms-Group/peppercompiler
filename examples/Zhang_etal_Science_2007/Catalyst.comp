declare component Catalyst: scat -> ssb + sob + scat

# ob and cat are sequence-independent, but ob's 3'-end 16nt must be identical to cat's 5'-end 16nt.
# sequences are defined with mostly-three-letter alphabets to be compatible with Dave's actual sequences.

## Sequences
sequence r1  = "10H" : 10
sequence r2  = "24N" : 24
sequence r3  = "4H" : 4
sequence r4  = "16N" : 16
sequence r5  = "6H" : 6
sequence r6  = "16H" : 16

## Sequences for system constraints
sequence scat = r4 r5
sequence ssb = r6 r3 r4
sequence sob = r1 r2

## Strands
# Inputs
strand fuel = r2 r3 r4
strand cat  = r4 r5
# Gate sections
strand lb = r5* r4* r3* r2*
strand ob = r1  r2
strand sb = r6  r3  r4

## Structures/Complexes
# Input
structure Fuel = fuel : domain ...
structure Cat  = cat  : domain ..
# Gate (and Intermediate structures)
structure S  = lb + ob + sb  : domain .(((+.)+.))
structure I3 = lb + ob + cat : domain ((.(+.)+))
# Outputs
structure SB = sb : domain ...
structure OB = ob : domain ..
# Waste
structure Waste = lb + fuel : domain .(((+)))

## Kinetics
# Overall: Cat + S + Fuel  ->  SB + OB + Cat + Waste
kinetic Cat  + S  ->  SB + I3
kinetic Fuel + I3 ->  OB + Waste + Cat

