function HalfAdder: x0 + y0 -> s0 + c1

## Sequences
sequence tx0  =  ?N : 6
sequence ty0  =  ?N : 6
sequence ts0  =  ?N : 6
sequence tc1  =  ?N : 6
sequence dx0  =  S ?N S : 15
sequence dy0  =  S ?N S : 30
sequence ds0  =  S ?N S : 15
sequence dc1  =  S ?N S : 30

## Super-sequences
sup-sequence  x0 =  tx0 dx0 : 21
sup-sequence  y0 =  dy0 ty0 : 36
sup-sequence  s0 =  ts0 ds0 : 21
sup-sequence  c1 =  dc1 tc1 : 36


## Strands
# Inputs
strand inX0 = x0* : 21
strand inY0 = y0* : 36

# Gate sections
strand X0 = x0 : 21
strand C1 = ?N c1* : 44
strand S0 = s0* ?N : 29
strand Y0 = y0  ?N : 45


## Structures/Complexes
# Input
structure InX0 = inX0 : U21
structure InY0 = inY0 : U36

# Gate (and partial structure after inX0 binds)
structure G0  = X0 + C1 + S0 + Y0 : U6 H15(+ H15(U29 + U14 H15(+)))
structure G0a =      C1 + S0 + Y0 :          H15(U29 + U14 H15(+)) U15

# Paired inputs (waste)
structure --no-mfe X0a = inX0 + X0 : H21(+)
structure --no-mfe Y0a = inY0 + Y0 : H36(+) U9

# Outputs
structure OutC1 = C1 : U44
structure OutS0 = S0 : U29


## Kinetics
# Overall: InX0 + InY0 + G0  ->  X0a + Y0a + OutC1 + OutS0
kin InX0 + G0  -> X0a + G0a
kin InY0 + G0a -> Y0a + OutC1 + OutS0

